#!/usr/bin/env perl
### pughlab_rnaseq_pipeline.pl #####################################################################
use AutoLoader 'AUTOLOAD';
use strict;
use warnings;
use Carp;
use POSIX qw(strftime);
use Getopt::Std;
use Getopt::Long;
use File::Basename;
use File::Path qw(make_path);
use YAML qw(LoadFile);

my $cwd = dirname($0);
require "$cwd/scripts/utilities.pl";

####################################################################################################
# version       author	  	comment
# 1.0		sprokopec       script to run PughLab RNASeq pipeline

### USAGE ##########################################################################################
# pughlab_rnaseq_pipeline.pl -c tool_config.yaml -d data.yaml
#
# where:
#	- tool_config.yaml contains tool versions and parameters, output directory, reference
#	information, etc.
#	- data_config.yaml contains sample information (YAML file containing paths to FASTQ files,
#	generated by create_fastq_yaml.pl)

### SUBROUTINES ####################################################################################

### MAIN ###########################################################################################
sub main {
	my %args = (
		tool_config	=> undef,
		data_config	=> undef,
		cleanup		=> undef,
		cluster		=> undef,
		dry_run		=> undef,
		@_
		);

	my $tool_config = $args{tool_config};
	my $data_config = $args{data_config};

	### PREAMBLE ######################################################################################

	# load tool config
	my $tool_data = LoadFile($tool_config);
	my $date = strftime "%F", localtime;
	my $timestamp = strftime "%F_%H-%M-%S", localtime;

	# check for and/or create output directories
	my $output_directory = $tool_data->{output_dir};
	$output_directory =~ s/\/$//;
	my $log_directory = join('/', $output_directory, 'logs', 'run_RNA_pipeline_' . $timestamp);

	unless(-e $output_directory) { make_path($output_directory); }
	unless(-e $log_directory) { make_path($log_directory); }

	# start logging
	my $log_file = join('/', $log_directory, 'run_RNASeq_pipeline.log');
	open (my $log, '>', $log_file) or die "Could not open $log_file for writing.";

	print $log "---\n";
	print $log "Running PughLab RNA-Seq pipeline.\n";
	print $log "\n  Tool config used: $tool_config";
	print $log "\n    Output directory: $output_directory";
	print $log "\n  Sample config used: $data_config";
	print $log "\n---";

	### MAIN ###########################################################################################

	my ($fc_run_id, $star_run_id, $gatk_run_id, $vc_run_id, $rsem_run_id, $starfus_run_id) = '';

	# prepare directory structure
	my $fc_directory = join('/', $output_directory, 'FusionCatcher');
	my $star_directory = join('/', $output_directory, 'STAR');
	my $starfus_directory = join('/', $output_directory, 'STAR-Fusion');
	my $rsem_directory = join('/', $output_directory, 'RSEM');
	my $gatk_directory = join('/', $output_directory, 'GATK');
	my $vc_directory = join('/', $output_directory, 'HaplotypeCaller');

	# indicate YAML files for processed BAMs
	my $star_output_yaml = join('/', $star_directory, 'star_bam_config_' . $timestamp . '.yaml');
	my $gatk_output_yaml = join('/', $gatk_directory, 'gatk_bam_config_' . $timestamp . '.yaml');

	## run FusionCatcher pipeline
	unless(-e $fc_directory) { make_path($fc_directory); }

	if (defined($tool_data->{fusioncatcher_config})) {

		my $fc_command = join(' ',
			"perl $cwd/scripts/fusioncatcher.pl",
			"-o", $fc_directory,
			"-t", $tool_data->{fusioncatcher_config},
			"-d", $data_config,
			"-c", $args{cluster},
			"-p", $tool_data->{project_name}
			);

		if ($args{cleanup}) {
			$fc_command .= " --remove";
			}
		if ($args{dry_run}) {
			$fc_command .= " --dry_run";
			}

		# record command (in log directory) and then run job
		print $log "\n\nSubmitting job for fusioncatcher.pl\n";
		print $log "  COMMAND: $fc_command\n\n";

		$fc_run_id = `$fc_command`;
		sleep(5);
		}

	## run STAR-alignment pipeline
	unless(-e $star_directory) { make_path($star_directory); }

	if (defined($tool_data->{star_config})) {

		my $star_command = join(' ',
			"perl $cwd/scripts/star.pl",
			"-o", $star_directory,
			"-t", $tool_data->{star_config},
			"-d", $data_config,
			"-b", $star_output_yaml,
			"-c", $args{cluster},
			"-p", $tool_data->{project_name}
			);

		if ($args{cleanup}) {
			$star_command .= " --remove";
			}
		if ($args{dry_run}) {
			$star_command .= " --dry_run";
			}

		# record command (in log directory) and then run job
		print $log "Submitting job for star.pl\n";
		print $log "  COMMAND: $star_command\n\n";

		$star_run_id = `$star_command`;
		sleep(5);

		print $log ">>> Final STAR job id: $star_run_id\n\n";

		}

	## run STAR-Fusion pipeline
	unless(-e $starfus_directory) { make_path($starfus_directory); }

	if (defined($tool_data->{star_fusion_config})) {

		my $starfus_command = join(' ',
			"perl $cwd/scripts/star_fusion.pl",
			"-o", $starfus_directory,
			"-t", $tool_data->{star_fusion_config},
			"-d", $star_output_yaml,
			"-c", $args{cluster},
			"-p", $tool_data->{project_name},
			"--depends", $star_run_id
			);

		if ($args{cleanup}) {
			$starfus_command .= " --remove";
			}
		if ($args{dry_run}) {
			$starfus_command .= " --dry_run";
			}

		# record command (in log directory) and then run job
		print $log "Submitting job for star_fusion.pl\n";
		print $log "  COMMAND: $starfus_command\n\n";

		$starfus_run_id = `$starfus_command`;
		sleep(5);
		}

	## run RSEM pipeline
	unless(-e $rsem_directory) { make_path($rsem_directory); }

	if (defined($tool_data->{rsem_config})) {

		my $rsem_command = join(' ',
			"perl $cwd/scripts/rsem.pl",
			"-o", $rsem_directory,
			"-t", $tool_data->{rsem_config},
			"-d", $star_output_yaml,
			"-c", $args{cluster},
			"-p", $tool_data->{project_name},
			"--depends", $star_run_id
			);

		if ($args{cleanup}) {
			$rsem_command .= " --remove";
			}
		if ($args{dry_run}) {
			$rsem_command .= " --dry_run";
			}

		# record command (in log directory) and then run job
		print $log "Submitting job for rsem.pl\n";
		print $log "  COMMAND: $rsem_command\n\n";
		$rsem_run_id = `$rsem_command`;
		sleep(5);
		}

	## run GATK indel realignment/recalibration pipeline
	unless(-e $gatk_directory) { make_path($gatk_directory); }

	if (defined($tool_data->{gatk_config})) {

		my $gatk_command = join(' ',
			"perl $cwd/scripts/gatk.pl",
			"--rna",
			"-o", $gatk_directory,
			"-t", $tool_data->{gatk_config},
			"-d", $star_output_yaml,
			"-b", $gatk_output_yaml,
			"-c", $args{cluster},
			"--depends", $star_run_id
			);

		if ($args{cleanup}) {
			$gatk_command .= " --remove";
			}
		if ($args{dry_run}) {
			$gatk_command .= " --dry_run";
			}

		# record command (in log directory) and then run job
		print $log "Submitting job for gatk.pl\n";
		print $log "  COMMAND: $gatk_command\n\n";
		$gatk_run_id = `$gatk_command`;
		sleep(5);
		print $log ">>> Final GATK job id: $gatk_run_id\n\n";

		}

	## run GATK's HaplotypeCaller pipeline
	unless(-e $vc_directory) { make_path($vc_directory); }

	if (defined($tool_data->{haplotype_caller_config})) {

		my $vc_command = join(' ',
			"perl $cwd/scripts/haplotype_caller.pl",
			"--rna",
			"-o", $vc_directory,
			"-t", $tool_data->{haplotype_caller_config},
			"-d", $gatk_output_yaml,
			"-c", $args{cluster},
			"-p", $tool_data->{project_name},
			"--depends", $gatk_run_id
			);

		if ($args{cleanup}) {
			$vc_command .= " --remove";
			}
		if ($args{dry_run}) {
			$vc_command .= " --dry_run";
			}

		# record command (in log directory) and then run job
		print $log "Submitting job for haplotype_caller.pl\n";
		print $log "  COMMAND: $vc_command\n\n";
		$vc_run_id = `$vc_command`;
		sleep(5);
		}

	# finish up
	print $log "\nProgramming terminated successfully.\n\n";
	close $log;

	}

### GETOPTS AND DEFAULT VALUES #####################################################################
# declare variables
my ($tool_config, $data_config);
my $hpc_driver = 'slurm';
my ($remove_junk, $dry_run);

my $help;

# read in command line arguments
GetOptions(
	'h|help'	=> \$help,
	't|tool=s'	=> \$tool_config,
	'd|data=s'	=> \$data_config,
	'c|cluster=s'	=> \$hpc_driver,
	'remove'	=> \$remove_junk,
	'dry_run'	=> \$dry_run
	 );

if ($help) {
	my $help_msg = join("\n",
		"Options:",
		"\t--help|-h\tPrint this help message",
		"\t--data|-d\t<string> data config (yaml format)",
		"\t--tool|-t\t<string> tool config (yaml format)",
		"\t--cluster|-c\t<string> cluster scheduler (default: slurm)",
		"\t--remove\t<boolean> should intermediates be removed? (default: false)",
		"\t--dry_run\t<boolean> should jobs be submitted? (default: false)"
		);

	print $help_msg;
	exit;
	}

if (!defined($tool_config)) { die("No tool config file defined; please provide -t | --tool (ie, tool_config.yaml)"); }
if (!defined($data_config)) { die("No data config file defined; please provide -d | --data (ie, sample_config.yaml)"); }

# check for compatible HPC driver; if not found, change dry_run to Y
my @compatible_drivers = qw(slurm);
if ( (!any { /$hpc_driver/ } @compatible_drivers ) && (!$dry_run) ) {
	print "Unrecognized HPC driver requested: setting dry_run to true -- jobs will not be submitted but commands will be written to file.\n";
	$dry_run = 1;
	}

main(
	tool_config	=> $tool_config,
	data_config	=> $data_config,
	cluster		=> $hpc_driver,
	cleanup		=> $remove_junk,
	dry_run		=> $dry_run
	);
